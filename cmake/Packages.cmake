set(CPACK_PROJECT_CONFIG_FILE "${CMAKE_SOURCE_DIR}/cmake/CPackProjectConfig.cmake")
set(CPACK_DEBIAN_PACKAGE_VERSION "${CMAKE_PROJECT_VERSION}")

set(PACKAGE_SUFFIX "-${CMAKE_PROJECT_VERSION}")

if(NOT PROJECT_VERSION_FINAL)
    set(PACKAGE_SUFFIX "${PACKAGE_SUFFIX}_${PROJECT_GIT_REV}")
    set(CPACK_DEBIAN_PACKAGE_VERSION "${CPACK_DEBIAN_PACKAGE_VERSION}~git.${PROJECT_GIT_REV}")
endif()

if(${CMAKE_SIZEOF_VOID_P} STREQUAL 4)
    set(PACKAGE_SUFFIX "${PACKAGE_SUFFIX}-x86")
elseif(${CMAKE_SIZEOF_VOID_P} STREQUAL 8)
    set(PACKAGE_SUFFIX "${PACKAGE_SUFFIX}-x86_64")
endif()

set(CPACK_ARCHIVE_DEADBEEF_PLUGIN_FILE_NAME     "${DEADBEEF_PACKAGE_NAME}${PACKAGE_SUFFIX}")
set(CPACK_ARCHIVE_FOOBAR2000_PLUGIN_FILE_NAME   "${FOOBAR2000_PACKAGE_NAME}${PACKAGE_SUFFIX}")

unset(PACKAGE_SUFFIX)

set(CPACK_STRIP_FILES ON)
set(CPACK_ARCHIVE_COMPONENT_INSTALL ON)
set(CPACK_DEB_COMPONENT_INSTALL ON)
set(CPACK_INCLUDE_TOPLEVEL_DIRECTORY OFF)

if(OS_WINDOWS)
    set(CPACK_GENERATOR ZIP)
else()
    set(CPACK_GENERATOR TXZ)
endif()

set(CPACK_DEBIAN_FILE_NAME DEB-DEFAULT)
set(CPACK_DEBIAN_PACKAGE_SHLIBDEPS ON)

set(CPACK_COMPONENTS_ALL "")
set(CPACK_ENABLE_DEADBEEF OFF)
set(CPACK_ENABLE_FOOBAR2000 OFF)

if(ENABLE_DEADBEEF)
    set(CPACK_ENABLE_DEADBEEF ON)
    list(APPEND CPACK_COMPONENTS_ALL deadbeef_plugin)
    set(CPACK_DEADBEEF_INSTALL_INTO_ROOT "${DEADBEEF_INSTALL_INTO_ROOT}")
endif()

if(ENABLE_FOOBAR2000)
    set(CPACK_ENABLE_FOOBAR2000 ON)
    list(APPEND CPACK_COMPONENTS_ALL foobar2000_plugin)
endif()
